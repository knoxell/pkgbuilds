# Maintainer: christian rebischke <chris.rebischke@archlinux.org>

pkgname=git-credential-gopass
pkgver=1.14.3
pkgrel=1
pkgdesc="git-credentials helper for gopass"
arch=('x86_64')
url="https://github.com/gopasspw/git-credential-gopass"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
_commit="5449acee6f4b9c05193d0148862bb5d584cde1b2"
source=("${pkgname}-${pkgver}::git+https://github.com/gopasspw/git-credential-gopass#commit=${_commit}")
sha512sums=('SKIP')
options=('!lto')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
