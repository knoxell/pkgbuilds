# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd
pkgver=2.4.9
# find matching argo-ui commit via ui/yarn.lock
# commit id also needs to be updated in argo-ui.patch
_argoui_commit=fecc293fb19f4c00891d8470932fb4951e21834c
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes"
arch=('x86_64')
url='https://github.com/argoproj/argo-cd/'
license=('Apache')
makedepends=('go' 'python' 'yarn')
depends=('glibc')
provides=('argocd-cli')
replaces=('argocd-cli')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz"
        "argo-ui-${_argoui_commit}.tar.gz::https://github.com/argoproj/argo-ui/archive/${_argoui_commit}.tar.gz"
        "argo-ui.patch")
sha512sums=('de5963deb35c03c170a525063f6497cc6e4d2057b7e8ecd43d647c65a8269b7e14f338d09e44d92cd29191563cae97d982820e6e198233d017f429729ceccd92'
            '8b4b840a4d5492cf9aa81c0cde5d5e2763b4c3f929c93650f6362b0acf6d4deff860f5c876a77c54fcd666808eb0979891092ef40d4101619e21aacc6498fc37'
            '8c6e3b284aed3ee92965601a2958d8eaf0e018005317d948e45ea01a3bce75a53bac904c373b69c926d607c44db6105da8e2b1d1cecfce3a1ce1545d58132097')

prepare() {
  cd "argo-cd-$pkgver"

  patch -p0 -i ../argo-ui.patch

  go mod download
}

build() {
  cd "argo-cd-$pkgver"

  (
    cd ui
    yarn install --cache-folder "${srcdir}/yarn-cache"
    ARGO_VERSION="$pkgver" NODE_ENV='production' NODE_ONLINE_ENV='online' yarn build
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPROXY=off
  go build -o argocd \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/v2/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/v2/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/v2/common.gitTag=${pkgver} \
              -linkmode=external \
              -extldflags \"${LDFLAGS}\"" \
    ./cmd

  ./argocd completion bash > argocd.bash
  ./argocd completion zsh > argocd.zsh
}

package() {
  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
  ln -s argocd "$pkgdir/usr/bin/argocd-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-application-controller"
  ln -s argocd "$pkgdir/usr/bin/argocd-repo-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-cmp-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-dex"
  ln -s argocd "$pkgdir/usr/bin/argocd-notifications"
  ln -s argocd "$pkgdir/usr/bin/argocd-git-ask-pass"

  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"
}
